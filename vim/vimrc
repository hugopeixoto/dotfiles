" pathogen
execute pathogen#infect()

let g:jsx_ext_required = 0

" undo and swap file stuff
set directory=~/.vim/tmp/swap//
set undofile
set undodir=~/.vim/tmp/undo//


" dunno
set noshowmatch
set modeline


" interfacy stuff
set ruler
set relativenumber
set tabpagemax=100


" searching
set hlsearch
set incsearch
set wildmenu


" syntax and indentation
runtime macros/matchit.vim
syntax on
filetype plugin indent on

if has("autocmd")
  autocmd BufRead,BufNewFile *.rs setf rust
  autocmd BufRead,BufNewFile */.ssh/config.d/* setf sshconfig

  autocmd BufWinEnter * call Whitespace()

  autocmd VimResized * exe "normal \<c-w>="
endif

function! Whitespace()
  " autocmd BufWinEnter * match ExtraWhitespace /\(.\{1560\}\zs.\+\)/

  if (&ft != 'make')
    highlight LeadingTabs ctermbg=red guibg=red
    call matchadd('LeadingTabs', '^\t\+')
  endif

  highlight TrailingWhitespace ctermbg=blue guibg=blue
  call matchadd('TrailingWhitespace', '\s\+$')
endfunction

set autoindent
set backspace=indent,eol,start
set cinoptions+=t0
set expandtab
set foldlevelstart=99
set foldmethod=syntax
set nojoinspaces
set shiftwidth=2
set synmaxcol=500
set tabstop=2

let g:netrw_hide=1
let g:netrw_list_hide='.*\.un\~$'
let g:sh_noisk=1

function! FormatCode(lang)
  normal m`
  if (a:lang == 'go')
    exec ":%!gofmt"
  elseif (a:lang == 'json')
    exec ":%!python -mjson.tool"
  elseif (a:lang == 'c++')
    exec ":%!clang-format -style=LLVM"
  else
    normal "gg=G``"
  endif
  normal ``
endfunction


function! SelectaCommand(choice_command, vim_command)
  try
    let selection = system(a:choice_command . " | selecta")
  catch /Vim:Interrupt/
    redraw!
    return
  endtry
  redraw!
  exec a:vim_command . " " . selection
endfunction

function! RubyIsTest(filename)
  return match(a:filename, "_test.rb$") > 0
endfunction

function! RubyCounterpart(filename)
  if (RubyIsTest(a:filename))
    return substitute(substitute(a:filename, "^\\(\\./\\)\\?test/", "app/", ""), "_test.rb$", ".rb", "")
  else
    return substitute(substitute(a:filename, "^\\(\\./\\)\\?app/", "test/", ""), ".rb$", "_test.rb", "")
  endif
endfunction

function! RubyTest(filename)
  if (RubyIsTest(a:filename))
    return a:filename
  else
    return RubyCounterpart(a:filename)
  endif
endfunction

function! RunTest(lang, filename)
  exec ":w"

  if (a:lang == 'ruby')
    exec "!bin/rails test " . RubyTest(a:filename)
  elseif (a:lang == 'rust')
    exec "!cargo run"
  elseif (a:lang == 'c++')
    exec "!make"
  endif
endfunction

function! OpenCounterpart(lang, filename, vim_command)
  if (a:lang == "ruby")
    exec a:vim_command . " " . RubyCounterpart(a:filename)
  endif
endfunction


function! NextFile(choice_command, filename, vim_command, delta)
  if a:delta > 0
    let grep_dir = "-A" . a:delta
    let next_cmd = "tail -n1"
  else
    let grep_dir = "-B" . -a:delta
    let next_cmd = "head -n1"
  endif

  let grep_cmd = "egrep " . grep_dir . " '^\(\./\)?" . a:filename . "$'"

  let selection = system(a:choice_command . "|" . grep_cmd . "|" . next_cmd)

  exec a:vim_command . " " . selection
endfunction


" funky shortcuts
let mapleader = ","
let projectFilesLister = "find . -maxdepth 8 -type f -not -name '*.un~' \| egrep -wv 'node_modules|tmp|.git'"

noremap <Leader>. :w<CR>:!ruby -Itest %<CR>
noremap <Leader>r :source $MYVIMRC<CR>
noremap <Leader>f :call FormatCode(&ft)<CR>

noremap <Leader>ce :call OpenCounterpart(&ft, expand("%"), ":e")<CR>
noremap <Leader>cs :call OpenCounterpart(&ft, expand("%"), ":sp")<CR>
noremap <Leader>ct :call OpenCounterpart(&ft, expand("%"), ":tabnew")<CR>
noremap <Leader>cv :call OpenCounterpart(&ft, expand("%"), ":vs")<CR>

noremap <Leader>n :call NextFile(projectFilesLister, expand("%"), ":e", 1)<CR>
noremap <Leader>p :call NextFile(projectFilesLister, expand("%"), ":e", -1)<CR>

noremap <Leader>e :call SelectaCommand(projectFilesLister, ":e")<cr>
noremap <Leader>s :call SelectaCommand(projectFilesLister, ":sp")<cr>
noremap <Leader>t :call SelectaCommand(projectFilesLister, ":tabnew")<cr>
noremap <Leader>v :call SelectaCommand(projectFilesLister, ":vs")<cr>

noremap <cr> :call RunTest(&ft, expand("%"))<cr>
noremap <Space> <C-w><C-w>

let g:ale_cpp_clang_options = '-Wall -pedantic -Werror -std=c++17 -stdlib=libc++'
let g:ale_cpp_clangcheck_options = '-- -Wall -pedantic -Werror -std=c++17 -stdlib=libc++ -x c++'
